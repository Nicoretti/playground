default := "playground"

# Make unit files available for systemd
[group: 'service']
link:
    @sudo cp playground.service /etc/systemd/system/playground.service
    @sudo cp env.service /etc/systemd/system/env.service

# Remove unit files
[group: 'service']
unlink:
    @sudo rm /etc/systemd/system/playground.service
    @sudo rm /etc/systemd/system/env.service

# Updates systemd file's seen by systemd
[group: 'service']
update: unlink link
    @sudo systemctl daemon-reload

# Print status of service unit
[group: 'service']
status service=default:
    @sudo systemctl status {{service}}.service || true

# Start the service
[group: 'service']
start service=default:
    @sudo systemctl start {{service}}.service

# Show the journalctl log for the service
[group: 'service']
log service=default:
    # short version of options -xeu <unit>
    journalctl --catalog --pager-end --unit={{service}}.service

